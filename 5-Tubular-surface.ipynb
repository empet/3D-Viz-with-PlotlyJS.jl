{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## <center> Tubular and ribbon surface defined by Frenet-Serre equations of their spine curve</center>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**1. Tubular surface**"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "A tubular surface  is generated by a 3D curve, called spine, and a moving circle of radius r, with center on the spine and  included in planes orthogonal to curve.\n",
    "\n",
    "Tubular surfaces are associated to spines that are biregular, that is, they have a $C^2$ parameterization, $c:[a,b]\\to \\mathbb{R}^3$, with non-null and non-collinear velocity, and  acceleration, i.e.\n",
    "$\\dot{c}(t)\\times \\ddot{c}(t)\\neq 0$.\n",
    "A tube of prescribed [curvature](https://en.wikipedia.org/wiki/Curvature#Curvature_of_space_curves) and [torsion](https://en.wikipedia.org/wiki/Torsion_of_a_curve) is defined by a spine parameterized by the arc length, i.e. by\n",
    "a parameterization, $c(s)$, with constant speed, $||\\dot{c}(s)||=1$, and non-null acceleration, $\\ddot{c}(s)\\neq 0$, for all $s$.\n",
    "\n",
    "The given curvature and torsion, $\\kappa(s)$,  $\\tau(s)$, define the Frenet-Serre equations:\n",
    "$$\\begin{array}{lll}\n",
    "    \\dot{e}_1(s)&=&\\kappa(t)e_2(s)\\\\\n",
    "    \\dot{e}_2(s)&=&-\\kappa(s)e_1(s)+\\tau(s)e_3(s)\\\\\n",
    "    \\dot{e}_3(s)&=&-\\tau(s)e_2(s),\\\\\n",
    "    \\end{array} $$\n",
    "    \n",
    "where $e_1(s), e_2(s), e_3(s)$ are  respectively the unit vectors of tangent, principal normal and binormal along the curve.\n",
    "\n",
    "Frenet-Serre equations completed with the equation $ \\dot{c}(s)=e_1(s)$ define a system of ordinary differential equations, with  12 equations and 12 unknown functions. The last three\n",
    "coordinates of a solution represent the  discretized curve,  $c(s)$, starting from an initial point, with a prescribed Frenet frame at that point.\n",
    "\n",
    "We define below a tubular surface with highly oscillating curvature and constant torsion of the spine."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "A  tubular surface having as spine  a curve, $c(s)$, parameterized by the arclength, is defined as follows:\n",
    "$r(s,u)=c(s)+\\varepsilon(e_2(s)cos(u)+e_3(s)sin(u))$, $0<\\varepsilon <<1$, $u\\in[0, 2\\pi]$.\n",
    "$\\varepsilon$ is the radius of circles orthogonal to the spine."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "To each vectorial equation of the above system one associates three equations for the coordinates of the corresponding vectors:\n",
    "$$\\begin{array}{lll}\n",
    "e_1&=&(u[1], u[2], u[3])\\\\\n",
    "e_2&=&(u[4], u[5], u[6])\\\\\n",
    "e_3&=&(u[7], u[8], u[9])\\end{array}$$\n",
    "\n",
    "\n",
    "$(u[10], u[11], u[12])$,  are the coordinates of points  on the curve."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Calculate the spine curve of given curvature and torsion function, as well as the vectors of the Frenet frame along this curve, by integrating a system of 12 ODEs:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using DifferentialEquations\n",
    "using StaticArrays\n",
    "using PlotlyJS\n",
    "include(\"src/plotlyju.jl\");\n",
    "include(\"src/PlotlyColorSchemes.jl\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function Frenet_eq!(du, u, p, s)  #system of ODEs \n",
    "    du[1] = κ(s)*u[4]\n",
    "    du[2] = κ(s)*u[5]\n",
    "    du[3] = κ(s)*u[6]\n",
    "    du[4] = -κ(s)*u[1]+τ(s)*u[7]\n",
    "    du[5] = -κ(s)*u[2]+τ(s)*u[8]\n",
    "    du[6] = -κ(s)*u[3]+τ(s)*u[9]\n",
    "    du[7] = -τ(s)*u[4]\n",
    "    du[8] = -τ(s)*u[5]\n",
    "    du[9] = -τ(s)*u[6]\n",
    "    du[10] = u[1]\n",
    "    du[11] = u[2]\n",
    "    du[12] = u[3]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "κ(s) = 3*sin(s/10)*sin(s/10) #curvature function\n",
    "τ(s) = 0.35 #constant torsion\n",
    "\n",
    "u0 = [1, 0, 0, 0, 1, 0, 0, 0, 1, 0, 0, 0] #initial condition for the system of ODE\n",
    "tspan = (0.0,  150) #time interval of integration\n",
    "prob = ODEProblem(Frenet_eq!, u0, tspan)\n",
    "sol = solve(prob);\n",
    "s = 0:0.15:150; #discrete arclength to interpolate the solution \n",
    "vect_sol  = sol.(s);\n",
    "\n",
    "# convert from vector of vectors to vector of static 12-vectors\n",
    "vect_sol = Vector{SVector{12, Float64}}(vect_sol)\n",
    "#convert and reshape the solution to get a matrix of size (length(s), 12)\n",
    "matr_sol = reshape(reinterpret(Float64, vect_sol), (12, length(vect_sol)))';  \n",
    "\n",
    "e₂ =  matr_sol[:, 4:6]\n",
    "e₃ = matr_sol[:, 7:9]\n",
    "spine = matr_sol[:, 10:12]\n",
    "#evaluate points x, y, z on the tubular surface \n",
    "lu = 100\n",
    "u = collect(range(0, stop= 2π, length = lu))\n",
    "epsilon = 0.2\n",
    "# find the coordinates of the surface parameterization\n",
    "#r(s,u)=c(s)+epsilon(e_2(s)cos(u)+e_3(s)sin(u))\n",
    "x, y, z = [spine[:, k] .* ones(lu)' + \n",
    "           epsilon*(e₂[:, k] .* cos.(u)' + \n",
    "           e₃[:, k] .* sin.(u)') for k in 1:3];"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Plot the tubular surface colored according to the curvature of the spine, i.e. the color of curvature of the center of each circle perpendicular to the spine\n",
    "is assigned to the points of that circle, as well:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "tube = surface(x=x, y=y, z=z, \n",
    "               surfacecolor = κ.(s) .* ones(lu)', \n",
    "               colorbar_len=0.5)\n",
    "\n",
    "pl = plot(tube, Layout(width=800, height= 800,\n",
    "                       scene_camera_eye=attr(x=2, y=2, z=0.6)),\n",
    "                       style=plotlyju)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "![tubular](images/tubular_surf.png)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The same tube with the height (z-values) colormapped to the default colorscale (Plasma):"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "![tube-z-color](images/tube-z-color.png)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**2. Ribbon surface**"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Ribbon surfaces can be used for visualizing  protein structure. From the above approach we can derive such a surface.\n",
    "Namely, once the spine curve is defined by Frenet-Serre equations, a ribbon surface having this spine,  is parameterized by:\n",
    "$$r(s,t)= c(s)+te_3(s), \\quad t\\in[-a,a], a>0$$\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Our discrete ribbon is parameterized by (xr, yr, zr), where:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "a= 0.15\n",
    "t = LinRange(-a, a, 10)\n",
    "xr = spine[:, 1] * ones(size(t))'+ e₃[:,1]* t'\n",
    "yr = spine[:, 2] * ones(size(t))'+ e₃[:,2]* t'\n",
    "zr = spine[:, 3] * ones(size(t))'+ e₃[:,3]* t';"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "ribbon = surface(x=xr, y=yr, z=zr,\n",
    "                 colorscale=pl_viridis,\n",
    "                 colorbar_len=0.5)\n",
    "fig = Plot(ribbon, Layout(width=800, height= 800,\n",
    "                       scene_camera_eye=attr(x=2, y=2, z=0.6)),\n",
    "                       style=plotlyju)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "![ribbon](images/ribbon-surf.png)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.6.1",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
